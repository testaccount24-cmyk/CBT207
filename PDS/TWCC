         MACRO
&NAME    TWCC  &ESCAPE=YES,&ERASE=ALT,&RESET=NO,&PRNTFMT=NO,           *
               &PRNT=NO,&ALARM=NO,&KBUNLK=NO,&RSTMDT=NO
.*********************************************************************
.*
.*   TWCC - GENERATE THE 3270 WRITE CONTROL COMMAND.  OPTIONALLY,
.*          THE ESCAPE AND WRITE PREFIX CAN BE GENERATED, MAKING
.*          THE 3270 SUITABLE FOR USE WITH TPUT.
.*          THIS MACRO IS FIRST WHEN DEFINING A SCREEN.
.*
.* - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
.*                                                                  **
.*                                                                  **
.* MAP3270 V2.2.0 - SOFTWARE PACKAGE TO IMPLEMENT FULL SCREEN       **
.* IO FOR IBM 3270 CLASS TERMINALS.                                 **
.*                                                                  **
.*             COPYRIGHT (C) 2021  EDWARD G LISS                    **
.*                     EGLISS4024@GMAIL.COM                         **
.*                                                                  **
.* THIS PROGRAM IS FREE SOFTWARE: YOU CAN REDISTRIBUTE IT           **
.* AND/OR MODIFY IT UNDER THE TERMS OF THE GNU GENERAL PUBLIC       **
.* LICENSE AS PUBLISHED BY THE FREE SOFTWARE FOUNDATION,            **
.* EITHER VERSION 3 OF THE LICENSE, OR ANY LATER VERSION.           **
.*                                                                  **
.* THIS PROGRAM IS DISTRIBUTED IN THE HOPE THAT IT WILL BE          **
.* USEFUL, BUT WITHOUT ANY WARRANTY; WITHOUT EVEN THE IMPLIED       **
.* WARRANTY OF MERCHANTABILITY OR FITNESS FOR A PARTICULAR          **
.* PURPOSE.  SEE THE GNU GENERAL PUBLIC LICENSE FOR MORE            **
.* DETAILS.                                                         **
.*                                                                  **
.* PLEASE SEE HTTPS://WWW.GNU.ORG/LICENSES/ FOR A COPY OF THE       **
.* GNU GENERAL PUBLIC LICENSE.                                      **
.*                                                                  **
.* - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
.*                                                                  **
.* REVISION 1 (R1) - ADDED SUPPORT FOR ERASE ALTERNATE              **
.*                                                                  **
.*********************************************************************
         GBLA  &WCC
         AIF   (T'&NAME EQ 'O').NAMEOK
&NAME    EQU   *
.NAMEOK  ANOP
&WCC     SETA  0
         TFVERI B'00100111',&ESCAPE,'ESCAPE'
         AIF   ('&ESCAPE' EQ 'NO').NOESC
         DC    AL1(&WCC)  3270 ESCAPE
.NOESC   ANOP
         AIF   (T'&ERASE EQ 'O').ERASE                              R1
         AIF   ('&ERASE' NE 'ALT').ERASE                            R1
         DC    X'7E'        3270 WRITE ERASE ALTERNATE              R1
         AGO   .OTHER                                               R1
.ERASE   ANOP                                                       R1
&WCC     SETA  B'11110001'
         TFVERI B'00000100',&ERASE,'ERASE'
         DC    AL1(&WCC)  3270 WRITE(X'F1') OR WRITE/ERASE(X'F5')
.OTHER   ANOP                                                       R1
&WCC     SETA  0
.*
         TFVERI B'01000000',&RESET,'RESET'
         TFVERI B'00110000',&PRNTFMT,'PRNTFMT'
         TFVERI B'00001000',&PRNT,'PRNT'
         TFVERI B'00000100',&ALARM,'ALARM'
         TFVERI B'00000010',&KBUNLK,'KBUNLK'
         TFVERI B'00000001',&RSTMDT,'RSTMDT'
         DC    AL1(&WCC)  3270 WCC
.EXIT    ANOP
         MEND
